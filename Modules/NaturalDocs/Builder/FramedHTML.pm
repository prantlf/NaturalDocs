###############################################################################
#
#   Package: NaturalDocs::Builder::FramedHTML
#
###############################################################################
#
#   A package that generates output in HTML with frames.
#
#   All functions are called with Package->Function() notation.
#
###############################################################################

# This file is part of Natural Docs, which is Copyright © 2003 Greg Valure
# Natural Docs is licensed under the GPL


use strict;
use integer;

# XXX This is necessary to keep OptiPerl's live syntax checking from complaining.
# It shouldn't be, but I couldn't reproduce the bug for them easily and don't feel like going to great lengths to do so.
use NaturalDocs::Constants;

package NaturalDocs::Builder::FramedHTML;


#
#   Topic: Inherits
#
#   <NaturalDocs::Builder::HTMLBase>
#
use base 'NaturalDocs::Builder::HTMLBase';


###############################################################################
# Group: Implemented Interface Functions


#
#   Function: INIT
#
#   Registers the package with <NaturalDocs::Builder>.
#
sub INIT
    {
    NaturalDocs::Builder::Add(__PACKAGE__);
    };


#
#   Function: CommandLineOption
#
#   Returns the option to follow -o to use this package.  In this case, "html".
#
sub CommandLineOption
    {
    return 'framedhtml';
    };


#
#   Function: BuildFile
#
#   Builds the output file from the parsed source file.
#
#   Parameters:
#
#       sourcefile       - The name of the source file.
#       parsedFile      - An arrayref of the source file as <NaturalDocs::Parser::ParsedTopic> objects.
#
sub BuildFile #(sourceFile, parsedFile)
    {
    my ($self, $sourceFile, $parsedFile) = @_;

    my $outputDirectory = NaturalDocs::Settings::OutputDirectory($self);
    my $outputFile = $self->OutputFileOf($sourceFile);
    my $fullOutputFile = NaturalDocs::File::JoinPath($outputDirectory, $outputFile);


    # 99.99% of the time the output directory will already exist, so this will actually be more efficient.  It only won't exist
    # if a new file was added in a new subdirectory and this is the first time that file was ever parsed.
    if (!open(OUTPUTFILEHANDLE, '>' . $fullOutputFile))
        {
        NaturalDocs::File::CreatePath( NaturalDocs::File::NoFileName($fullOutputFile) );

        open(OUTPUTFILEHANDLE, '>' . $fullOutputFile)
            or die "Couldn't create output file " . $fullOutputFile . "\n";
        };

    print OUTPUTFILEHANDLE



        # IE 6 doesn't like any doctype here at all.  Add one (strict or transitional doesn't matter) and it makes the page slightly too
        # wide for the frame.  Mozilla and Opera handle it like champs either way because they Don't Suck(tm).

        # '<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.0 Transitional//EN" '
        # . '"http://www.w3.org/TR/REC-html40/loose.dtd">' . "\n\n"

        '<html><head>'

            . '<title>'
                . $self->BuildTitle($sourceFile)
            . '</title>'

            . '<link rel="stylesheet" type="text/css" href="'. $self->MakeRelativeURL($outputFile, 'NaturalDocs.css') . '">'

            . $self->BrowserStylesJavaScript()
            . $self->ToolTipsJavaScript()

        . '</head><body class=FramedContentPage>'
            . $self->OpeningBrowserStyles() . "\n\n"

            . '<!--  Generated by Natural Docs, version ' . NaturalDocs::Settings::TextAppVersion() . ' -->' . "\n"
            . '<!--  ' . NaturalDocs::Settings::AppURL() . '  -->' . "\n\n"

            . $self->BuildContent($sourceFile, $parsedFile)

            . $self->BuildToolTips()

            . $self->ClosingBrowserStyles()
        . '</body></html>';


    close(OUTPUTFILEHANDLE);
    };


#
#   Function: BuildIndex
#
#   Builds an index for the passed type.
#
#   Parameters:
#
#       type  - The type to limit the index to, or undef if none.
#
sub BuildIndex #(type)
    {
    my ($self, $type) = @_;

    my $indexTitle = $self->IndexTitleOf($type);
    my $indexFile = $self->IndexFileOf($type);

    my $startPage =

        '<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.0 Transitional//EN" '
            . '"http://www.w3.org/TR/REC-html40/loose.dtd">' . "\n\n"

        . '<html><head>'

            . '<title>';

            if (defined NaturalDocs::Menu::Title())
                {  $startPage .= $self->StringToHTML(NaturalDocs::Menu::Title()) . ' - ';  };

                $startPage .=
                $indexTitle
            . '</title>'

            . '<link rel="stylesheet" type="text/css" href="'. $self->MakeRelativeURL($indexFile, 'NaturalDocs.css') . '">'

            . $self->BrowserStylesJavaScript()
            . $self->ToolTipsJavaScript()

        . '</head><body class=FramedIndexPage>'
            . $self->OpeningBrowserStyles() . "\n\n"

            . '<!--  Generated by Natural Docs, version ' . NaturalDocs::Settings::TextAppVersion() . ' -->' . "\n"
            . '<!--  ' . NaturalDocs::Settings::AppURL() . '  -->' . "\n\n"

            . '<div class=IPageTitle>'
                . $indexTitle
            . '</div>';


    my $endPage = $self->ClosingBrowserStyles() . '</body></html>';


    my $index = NaturalDocs::SymbolTable::Index($type);
    my $indexContent = $self->BuildIndexContent($index, $indexFile);
    my $indexPages = $self->BuildIndexFiles($type, $indexContent, $startPage, $endPage);
    $self->PurgeIndexFiles($type, $indexPages + 1);
    };


#
#   Function: UpdateMenu
#
#   Builds the menu file.  Also generates index.html.
#
sub UpdateMenu
    {
    my $self = shift;

    my $outputDirectory = NaturalDocs::Settings::OutputDirectory($self);
    my $outputFile = NaturalDocs::File::JoinPath($outputDirectory, 'menu.html');


    open(OUTPUTFILEHANDLE, '>' . $outputFile)
        or die "Couldn't create output file " . $outputFile . "\n";

    my $title = NaturalDocs::Menu::Title();
    if (defined $title)
        {  $title .= ' - Menu';  }
    else
        {  $title = 'Menu';  };

    $title = $self->StringToHTML($title);


    print OUTPUTFILEHANDLE


        '<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.0 Transitional//EN" '
            . '"http://www.w3.org/TR/REC-html40/loose.dtd">' . "\n\n"

        . '<html><head>'

            . '<title>'
                . $title
            . '</title>'

            . '<base target="Content">'

            . '<link rel="stylesheet" type="text/css" href="NaturalDocs.css">'

            . $self->MenuToggleJavaScript()
            . $self->BrowserStylesJavaScript()

        . '</head><body class=FramedMenuPage>'
            . $self->OpeningBrowserStyles() . "\n\n"

            . '<!--  Generated by Natural Docs, version ' . NaturalDocs::Settings::TextAppVersion() . ' -->' . "\n"
            . '<!--  ' . NaturalDocs::Settings::AppURL() . '  -->' . "\n\n"

            . $self->BuildMenu('menu.html', 1)

            . '<div class=Footer>'
                . $self->BuildFooter()
            . '</div>'

            . $self->ClosingBrowserStyles()
        . '</body></html>';


    close(OUTPUTFILEHANDLE);


    # Update index.html

    my $firstMenuEntry = $self->FindFirstFile();
    my $indexFile = NaturalDocs::File::JoinPath( NaturalDocs::Settings::OutputDirectory($self), 'index.html' );

    # We have to check because it's possible that there may be no files with Natural Docs content and thus no files on the menu.
    if (defined $firstMenuEntry)
        {
        open(INDEXFILEHANDLE, '>' . $indexFile)
            or die "Couldn't create output file " . $indexFile . ".\n";

        print INDEXFILEHANDLE

            '<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.0 Frameset//EN" '
                . '"http://www.w3.org/TR/REC-html40/frameset.dtd">'

            . '<html>'

                . '<head>'

                    . '<title>'
                        . $self->StringToHTML(NaturalDocs::Menu::Title())
                    . '</title>'

                . '</head>'

                . '<frameset cols="185,*">'
                    . '<frame name=Menu src="menu.html">'
                    . '<frame name=Content src="'
                        . $self->MakeRelativeURL('index.html', $self->OutputFileOf($firstMenuEntry->Target())) . '">'
                . '</frameset>'

                . '<noframes>'
                    . 'This documentation was designed for use with frames.  However, you can still use it by '
                    . '<a href="menu.html">starting from the menu page</a>.'
                    . "<script language=JavaScript><!--\n"
                        . 'location.href="menu.html";'
                    . "\n// --></script>"
                . '</noframes>'

            . '</html>';

        close INDEXFILEHANDLE;
        }

    elsif (-e $indexFile)
        {
        unlink($indexFile);
        };
    };


1;